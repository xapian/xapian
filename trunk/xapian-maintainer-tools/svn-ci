#! /usr/bin/perl -w
# Wrap "svn ci" and help generate ChangeLog entries.
#
# This script takes an optional list of files and/or directories, just like
# "svn ci".
#
# If you save this somewhere on your PATH, you can just run "svn-ci" instead
# of "svn ci" when you want to check in a change to the Xapian tree.

require 5.000;
use strict;
use POSIX;

# Set these if you don't like the default values
my $email;
my $realname;

###############################################################################

if (defined $ARGV[0] && $ARGV[0] eq '--help') {
    print <<END;
Syntax: $0 [FILE]...

The default is to check in all changed files in the current directory or
its subdirectories.

If there is no ChangeLog in the current directory, $0 will look for one
in the parent directory and above.

If ChangeLog has already been updated and doesn't contain evidence of
conflicts or diff fragments, check in the requested files plus ChangeLog.

If ChangeLog hasn't updated, a suitable entry header is prepended, plus
the diffs between CVS and the files to be checked in, and the user is
prompted to edit it.

If ChangeLog contains evidence of conflicts or diff fragments, the user
is prompted to edit it.
END
    exit 0;
}

# Set default values if the user hasn't set their own above...
my @passwd = getpwuid(getuid());
unless ($email) {
    $email = $passwd[0];
    if ($email eq 'olly') {
	$email = 'olly@survex.com';
    } elsif ($email eq 'richard') {
	$email = 'richard@lemurconsulting.com';
    } else {
	my $host = `hostname -d`;
	chomp($host);
	$email = $passwd[0] . '@' . $host;
    }
}
$realname ||= (split /,/, $passwd[6])[0];

# Remove any trailing slashes on directories for consistency.
@ARGV = map {s,/+$,,; $_} @ARGV;

if (! -f "ChangeLog") {
    my $path = "";
    for (reverse(split m!/!, getcwd())) {
	chdir "..";
	$path = "$_/$path";
	last if -f "ChangeLog";
    }
    if (scalar @ARGV) {
	@ARGV = map {$path.$_} @ARGV;
    } else {
	chop $path;
	push @ARGV, $path;
    }
}

my $tmp = "ChangeLog.$$.new";

if (scalar @ARGV) {
   push @ARGV, "ChangeLog" unless grep {$_ eq "ChangeLog"} @ARGV;
}

my $edit_cl = 0;
my $add_diff = 1;

# FIXME: ought to search down and then up for one...
# Searching down should check-in with the same message in each subdir
# Searching up should check-in using the first changelog in a parent dir
# but only files below this directory...
open I, "<ChangeLog" or die "No ChangeLog found in this directory or a parent directory!\n";
while (<I>) {
    # conflicts, or diff fragments
    if (/^[-<+]/) {
        $edit_cl = 1;
	$add_diff = 0;
	last;
    }
}
close I;

if (!$edit_cl) {
    system("svn diff ChangeLog");
    my $exit = ($? >> 8);
    if ($exit != 0 && $exit != 1) {
	die "svn diff failed ($?)\n";
    }
    if ($exit == 0) {
	$edit_cl = 1;
    }
    if (!$edit_cl) {
	# FIXME: ChangeLog changed - check if they want to edit it ?
    }
}

if ($edit_cl) {
    if ($add_diff) {
	# ChangeLog unchanged - add diff to top
	open CO, ">$tmp" or die $!;
	open CI, "svn diff ".join(" ", map quotemeta, @ARGV)."|";
	my @files;
	my $diff = '';
	while (<CI>) {
	    $diff .= $_;
	    if (/^Index: (.*)/) {
		push @files, $1;
	    }
	} 
	my $d = "";
	my @newfiles;
	my @d;
	for (sort @files) {
	    my $dir = $d;
	    if (m!^(.+/)!) {
		$d = $1;
		if ($d eq $dir) {
		    push @d, $_;
		    next;
		}
	    } else {
		$d = "";
	    }
	    if (scalar @d > 3 && length $dir) {
		push @newfiles, $dir;
	    } else {
		push @newfiles, @d;
	    }
	    if (length $d) {
		@d = ($_);
	    } else {
		@d = ();
		push @newfiles, $_;
	    }
	}
	if (scalar @d > 3) {
	    push @newfiles, $d;
	} else {
	    push @newfiles, @d;
	}
	print CO strftime("%a %h %d %H:%M:%S %Z %Y", localtime);
	print CO "  $realname <$email>\n\n\t* ";
	my $line = join ',', @newfiles;
	while (length($line) >= 68 && $line =~ s/^(.{1,68},)//) {
	    print CO "$1\n\t  ";
	}
	print CO "$line:\n\n\n";
	print CO $diff;
	print CO "\n";
	open CI, "<ChangeLog" or die $!;
	while (<CI>) {
	    print CO;
	}
	close CI;
	close CO;
	rename $tmp, "ChangeLog";
    }
    system(($ENV{VISUAL}||$ENV{EDITOR}||'vi'), "ChangeLog");
    ($? >> 8 == 0) || die "$?\n";
}

my $msg = '';

open CI, "<ChangeLog" or die $!;
while (<CI>) {
    # conflicts, or diff fragments
    if (/^[-<+]/) {
	bad_cl();
    }
    last if /^$/;
}
while (<CI>) {
    # conflicts, or diff fragments
    if (/^[-<+]/) {
	bad_cl();
    }
    last if /^\w/;
    $msg .= $_;
}
while (<CI>) {
    # conflicts, or diff fragments
    if (/^[-<+]/) {
	bad_cl();
    }
}
close CI;
$msg =~ s/\s+$//s;
$msg =~ s/^\s+(?:\* \b)?//mg;
system("svn", "ci", "-m", $msg, @ARGV);

sub bad_cl {
    if (/^</) {
	print STDERR "Unresolved conflicts in ChangeLog\n";
    } else {
	print STDERR "Diff fragments in ChangeLog\n";
    }
    print STDERR "Rerunning the ci command will give you a chance to edit ChangeLog\n";
    exit 1;
}
