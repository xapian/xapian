dnl Process this file with autoconf to produce a configure script.

dnl Need autoconf 2.50 or later for many features
dnl 2.54 needed for automake 1.6
AC_PREREQ(2.54)
AC_INIT(xapian-bindings, 0.7.4)dnl FIXME: bugreport addr as third argument
AM_INIT_AUTOMAKE
AC_CONFIG_SRCDIR(xapian.i)

AM_CONFIG_HEADER(config.h)

dnl Use libtool to manage our libraries
AC_PROG_LIBTOOL

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX

dnl Check for libxapian
XO_LIB_XAPIAN(, AC_MSG_ERROR([Can't find Xapian library]))

dnl Check for swig - this does most of the work for the bindings.
AC_PATH_PROG(SWIG, swig, [])
if test -z "$SWIG" ; then
  AC_MSG_ERROR([Can't find SWIG utility])
fi
dnl Check for swig >= 1.3.14
dnl James says:
dnl  1.3.18 -> 1.3.19 was very quick, and 1.3.15 through 1.3.17
dnl  were mostly bugfixes on 1.3.14, so 1.3.14 might be sufficient. 1.3.12
dnl  introduced support for C++ namespaces; you need 1.3.14 for overloaded
dnl  functions (which we use).
v="`$SWIG -version 2>&1`"
case "`echo \"$v\"|sed 's/^SWIG Version \([[0-9\.]]*\).*/\1/p;d'`" in
 [1.3.1[4-9]|1.3.[2-9][0-9]|1.3.[1-9][0-9][0-9]*|1.[4-9].*|2.*]) ;;
 *)
   AC_MSG_ERROR([SWIG >= 1.3.14 required (you have $v)]) ;;
esac

BINDINGS=
AC_PATH_PROG(PYTHON, python, [])
if test -n "$PYTHON" ; then
    PYTHON_INC="`python -c 'import sys;print sys.prefix + "/include/python" + sys.version[[:3]]'`"
    AC_SUBST(PYTHON_INC)
    PYTHON_LIB="`python -c 'import sys;print sys.prefix + "/lib/python" + sys.version[[:3]]'`"
    AC_SUBST(PYTHON_LIB)
    BINDINGS="$BINDINGS python"
fi
AC_PATH_PROG(PHP_CONFIG, php-config, [])
if test -n "$PHP_CONFIG" ; then
    dnl FIXME: check version: $PHP_CONFIG --version returns e.g. "4.2.1"
    PHPIZE="`$PHP_CONFIG --prefix`/bin/phpize"
    AC_SUBST(PHPIZE)
    BINDINGS="$BINDINGS php4"
fi
dnl FIXME: also check for guile, tcl8, java

AC_SUBST(BINDINGS)

AC_CONFIG_FILES([
 Makefile

 python/Makefile
 python/docs/Makefile
 python/docs/examples/Makefile

 php4/Makefile
 guile/Makefile
 tcl8/Makefile])
AC_OUTPUT
